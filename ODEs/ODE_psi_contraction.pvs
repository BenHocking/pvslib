ODE_psi_contraction [ d :[ real -> bool ] ]  % Welcome
% d - domain of the functions r2v
		: THEORY

%Created by                     JTS
%	 	           Dec 2019
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%|Shows that psi is a contraction|%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%Last Updated	           Jan 2020
%			        JTS

%-----     %
  BEGIN
%     -----%

%-------------------------------------------------------------------------
%% Assume that the domain is a closted interval
%-------------------------------------------------------------------------

  ASSUMING

    IMPORTING interval_def
      closed_interval : ASSUMPTION closed_interval?(d)

  ENDASSUMING

%-------------------------------------------------------------------------
%% Importing exp_analysis to have derivative and integral of exp(ax-b)
%% Importing metric_rho to have rho
%% Importing norm_ineq to have the norm of intergral is less than the
%  integral of the norm
%% Importing ODE_psi_def[d] to have definition of psi 
%-------------------------------------------------------------------------

IMPORTING exp_analysis[d]
IMPORTING metric_rho[d]
IMPORTING norm_ineq[d]
IMPORTING ODE_psi_def[d]

%-------------------------------------------------------------------------
%% Breaking it up the proof that psi is a contraction into smaller steps
%  these steps are very problem specific, and are very easy to describe
%  in a useful way
%-------------------------------------------------------------------------

%-------------------------------------------------------------------------
%% Difference of od_fx functions is Integrable
%-------------------------------------------------------------------------

od_fx_diff_Int: LEMMA
 FORALL (n: posnat,
  f: {ff: od_f[d](n) | Lip_d?[d](n)(ff) AND cont_d?[d](n)(ff)},
  y, z: {yy: r2v_n[d](n) | continuous?[d](yy)},
  xx: (d)):
   Integrable?[d]
    (car[real](d_b[d]), xx,
    od_fx[d](n, f, y) - od_fx[d](n, f, z))

%-------------------------------------------------------------------------
%% The norm of the difference of od_fx functions is continuous
%-------------------------------------------------------------------------

od_fx_contD_diff: LEMMA
 FORALL (n: posnat,
   f: {ff: od_f[d](n) | Lip_d?[d](n)(ff) AND cont_d?[d](n)(ff)},
   y, z: {yy: r2v_n[d](n) | continuous?[d](yy)}, K: posreal,
   nn: (norm?(n))):
     contD?[1]
       (LAMBDA (x: VectorN(1)): d(car[real](x)))
         (LAMBDA (x: VectorN(1)):
           ext_c[d](LAMBDA (xx: (d)):
             exp(-K * (xx - car[real](d_b[d]))) *
             nn(Integral[d](car[real](d_b[d]), xx,
             (od_fx[d](n, f, y) -  od_fx[d](n, f, z)))))
             (car[real](x)))

%-------------------------------------------------------------------------
%% The function rho(psi,psi) is the max of the norm of the integral of
%  the difference of od_fx functions
%-------------------------------------------------------------------------

rho_psi_int: LEMMA
 FORALL(n:posnat, y0: VectorN(n),
   f: {ff: od_f[d](n) | Lip_d?[d](n)(ff) AND cont_d?(n)(ff)},
   y,z:{yy:r2v_n(n)|continuous?(yy)},K:posreal, nn:(norm?(n))):
     rho(n,K,nn)(psi(n,car(d_b),y0,y,f),psi(n,car(d_b),y0,z,f))
     = max(LAMBDA(x:VectorN(1)):d(car(x)))
       (LAMBDA(x:VectorN(1)):
       ext_c(LAMBDA(xx:(d)): exp(-K*(xx-car(d_b))) *
       nn(Integral(car(d_b),xx,od_fx(n,f,y)-od_fx(n,f,z))))(car(x)))

%-------------------------------------------------------------------------
%% The norm of the difference of od_fx functions is integrable
%-------------------------------------------------------------------------

od_fx_diff_norm_int: LEMMA
 FORALL (n: posnat,
   f: {ff: od_f[d](n) | Lip_d?[d](n)(ff) AND cont_d?[d](n)(ff)},
   y, z: {yy: r2v_n[d](n) | continuous?[d](yy)},
   xx: (d)):
     Integrable?[(d)](car[real](d_b[d]), xx,
       LAMBDA (dd: (d)):
         norm_inf(n)
	   (od_fx[d](n, f, y)(dd) - od_fx[d](n, f, z)(dd)))

%-------------------------------------------------------------------------
%% The integral of the norm of the difference of od_fx functions
%  is continuous (with exponential term)
%-------------------------------------------------------------------------

od_fx_contD_Int_diff: LEMMA
  FORALL (n: posnat,
    f: {ff: od_f[d](n) | Lip_d?[d](n)(ff) AND cont_d?[d](n)(ff)},
    y, z: {yy: r2v_n[d](n) | continuous?[d](yy)}, K: posreal):
      contD?[1](LAMBDA (x: VectorN(1)): d(car[real](x)))
        (LAMBDA (x: VectorN(1)):
        ext_c[d](LAMBDA (xx: (d)):
          exp(-K * (xx - car[real](d_b[d]))) *
          Integral[(d)](car[real](d_b[d]), xx,
          LAMBDA (dd: (d)):norm_inf(n)
          (od_fx[d](n, f, y)(dd) -
          od_fx[d](n, f, z)(dd))))
            (car[real](x)))

%-------------------------------------------------------------------------
%% The function rho(psi,psi) is less than the max of integral of
%  the norm of od_fx functions (with exponential term)
%-------------------------------------------------------------------------

rho_psi_int_ineq: LEMMA
 FORALL(n:posnat, y0: VectorN(n),
   f: {ff: od_f[d](n) | Lip_d?[d](n)(ff) AND cont_d?(n)(ff)},
   y,z:{yy:r2v_n(n)|continuous?(yy)},K:posreal):
     rho(n,K,norm_inf(n))(psi(n,car(d_b),y0,y,f),psi(n,car(d_b),y0,z,f))
     <= max(LAMBDA(x:VectorN(1)):d(car(x)))
    (LAMBDA(x:VectorN(1)):
    ext_c(LAMBDA(xx:(d)): exp(-K*(xx-car(d_b)))
    * Integral(car(d_b),xx,
    LAMBDA(dd:(d)):norm_inf(n)(od_fx(n,f,y)(dd)-od_fx(n,f,z)(dd))))(car(x)))

%-------------------------------------------------------------------------
%% The norm of the difference of continuous functions is Integrable
%-------------------------------------------------------------------------

norm_diff_Int: LEMMA
 FORALL (n: posnat,
   y, z: {yy: r2v_n[d](n) | continuous?[d](yy)},
   xx: (d)):
     Integrable?[(d)]
       (car[real](d_b[d]), xx,
         LAMBDA (dd: (d)):
	   norm_inf(n)(y(dd) - z(dd)))
	   
%-------------------------------------------------------------------------
%% The Integral of the norm of the difference of continuous functions
%  is continuous (with exponential term)
%-------------------------------------------------------------------------

norm_diff_contD: LEMMA
  FORALL (n: posnat,
  y, z: {yy: r2v_n[d](n) | continuous?[d](yy)}, K:posreal):
    contD?[1]
    (LAMBDA (x: VectorN(1)): d(car[real](x)))
    (LAMBDA (x: VectorN(1)):
      ext_c[d]
        (LAMBDA (xx: (d)):
         exp(-K * (xx - car[real](d_b[d]))) *
         Integral[(d)]
           (car[real](d_b[d]), xx,
             LAMBDA (dd: (d)):
	     norm_inf(n)(y(dd) - z(dd))))
             (car[real](x)))
	     
%-------------------------------------------------------------------------
%% Using the fact that f is Lipschitz to get bound involving Lip_c(n)(f)
%-------------------------------------------------------------------------

rho_psi_lip_ineq: LEMMA
 FORALL(n:posnat, y0: VectorN(n),
   f: {ff: od_f[d](n) | Lip_d?[d](n)(ff) AND cont_d?(n)(ff)},
   y,z:{yy:r2v_n(n)|continuous?(yy)},K:posreal):
     rho(n,K,norm_inf(n))(psi(n,car(d_b),y0,y,f),psi(n,car(d_b),y0,z,f))
     <= Lip_c(n)(f) * max(LAMBDA(x:VectorN(1)):d(car(x)))
    (LAMBDA(x:VectorN(1)):
    ext_c(LAMBDA(xx:(d)): exp(-K*(xx-car(d_b)))
    * Integral(car(d_b),xx,LAMBDA(dd:(d)):norm_inf(n)(y(dd) - z(dd))))(car(x)))

%-------------------------------------------------------------------------
%% Insert an exponential term and its reciprocal inside the integral
%  (first showing it is continuous)
%-------------------------------------------------------------------------

rho_psi_exp_contD_triv: LEMMA
 FORALL (n: posnat, y0: VectorN(n),
   f: {ff: od_f[d](n) | Lip_d?[d](n)(ff) AND cont_d?[d](n)(ff)},
   y, z: {yy: r2v_n[d](n) | continuous?[d](yy)}, K: posreal):
    contD?[1](LAMBDA (x: VectorN(1)): d(car[real](x)))
     (LAMBDA (x: VectorN(1)):
      ext_c[d](LAMBDA (xx: (d)):
        exp(-K * (xx - car[real](d_b[d]))) *
         Integral[(d)](car[real](d_b[d]), xx,
          LAMBDA (dd: (d)): exp(K * (dd - car[real](d_b[d]))) *
           (exp(-K * (dd - car[real](d_b[d]))) *
            norm_inf(n)(y(dd) - z(dd)))))
                   (car[real](x)))

rho_psi_int_exp: LEMMA
 FORALL(n:posnat, y0: VectorN(n),
   f: {ff: od_f[d](n) | Lip_d?[d](n)(ff) AND cont_d?(n)(ff)},
   y,z:{yy:r2v_n(n)|continuous?(yy)},K:posreal):
     rho(n,K,norm_inf(n))(psi(n,car(d_b),y0,y,f),psi(n,car(d_b),y0,z,f))
     <=  Lip_c(n)(f) * max(LAMBDA(x:VectorN(1)):d(car(x)))
    (LAMBDA(x:VectorN(1)):
    ext_c(LAMBDA(xx:(d)): exp(-K*(xx-car(d_b)))
    * Integral(car(d_b),xx,LAMBDA(dd:(d)):
    exp(K*(dd-car(d_b)))*(exp(-K*(dd-car(d_b))) *
    norm_inf(n)(y(dd) - z(dd)))))(car(x)))

%-------------------------------------------------------------------------
%% The function rho multiplied by an expoential term is Integrable
%-------------------------------------------------------------------------

exp_rho_Integrable: LEMMA
 FORALL (n: posnat,
   y, z: {yy: r2v_n[d](n) | continuous?[d](yy)}, K: posreal,
   xx: (d)):
     Integrable?[(d)]
       (car[real](d_b[d]), xx,
       LAMBDA (dd: (d)):
       exp(K * (dd - car[real](d_b[d]))) *
       (rho[d](n, K, norm_inf(n))(y, z)))

%-------------------------------------------------------------------------
%% The Integral is continuous, with and without the function rho
%-------------------------------------------------------------------------

exp_rho_contD_rho: LEMMA
 FORALL (n: posnat,
   y, z: {yy: r2v_n[d](n) | continuous?[d](yy)}, K: posreal):
     contD?[1](LAMBDA (x: VectorN(1)): d(car[real](x)))
       (LAMBDA (x: VectorN(1)):
       ext_c[d](LAMBDA (xx: (d)):
       exp(-K * (xx - car[real](d_b[d]))) *
       Integral[(d)](car[real](d_b[d]), xx,
       LAMBDA (dd: (d)):exp(K * (dd - car[real](d_b[d]))) *
        (rho[d](n, K, norm_inf(n))(y, z))))
          (car[real](x)))

exp_rho_contD: LEMMA
 FORALL (n: posnat, K: posreal):
    contD?[1](LAMBDA (x: VectorN(1)): d(car[real](x)))
      (LAMBDA (x: VectorN(1)):
        ext_c[d](LAMBDA (xx: (d)):
          exp(-K * (xx - car[real](d_b[d]))) *
            Integral[(d)](car[real](d_b[d]), xx,
             LAMBDA (dd: (d)): exp(K * (dd - car[real](d_b[d])))))
               (car[real](x)))

%-------------------------------------------------------------------------
%% Bounding the Integral with the function rho
%-------------------------------------------------------------------------

rho_psi_int_rho: LEMMA
 FORALL(n:posnat, y0: VectorN(n),
   f: {ff: od_f[d](n) | Lip_d?[d](n)(ff) AND cont_d?(n)(ff)},
   y,z:{yy:r2v_n(n)|continuous?(yy)},K:posreal):
     rho(n,K,norm_inf(n))(psi(n,car(d_b),y0,y,f),psi(n,car(d_b),y0,z,f))
     <=  Lip_c(n)(f) * max(LAMBDA(x:VectorN(1)):d(car(x)))
    (LAMBDA(x:VectorN(1)):
    ext_c(LAMBDA(xx:(d)): exp(-K*(xx-car(d_b)))
    * Integral(car(d_b),xx,LAMBDA(dd:(d)):
    exp(K*(dd-car(d_b)))*(rho(n,K,norm_inf(n))(y,z))))(car(x)))

%-------------------------------------------------------------------------
%% Pulling the function rho outside the integral
%-------------------------------------------------------------------------

rho_psi_rho_pull: LEMMA
 FORALL(n:posnat, y0: VectorN(n),
   f: {ff: od_f[d](n) | Lip_d?[d](n)(ff) AND cont_d?(n)(ff)},
   y,z:{yy:r2v_n(n)|continuous?(yy)},K:posreal):
     rho(n,K,norm_inf(n))(psi(n,car(d_b),y0,y,f),psi(n,car(d_b),y0,z,f))
     <=  (Lip_c(n)(f)*rho(n,K,norm_inf(n))(y,z)) *
     max(LAMBDA(x:VectorN(1)):d(car(x)))
    (LAMBDA(x:VectorN(1)):
    ext_c(LAMBDA(xx:(d)): exp(-K*(xx-car(d_b)))
    * Integral(car(d_b),xx,LAMBDA(dd:(d)):  exp(K*(dd-car(d_b)))))(car(x)))

%-------------------------------------------------------------------------
%% Solving the Integral that involves the e^ax-b term. 
%-------------------------------------------------------------------------

rho_psi_rho_exp_int: LEMMA
 FORALL(n:posnat, y0: VectorN(n),
   f: {ff: od_f[d](n) | Lip_d?[d](n)(ff) AND cont_d?(n)(ff)},
   y,z:{yy:r2v_n(n)|continuous?(yy)},K:posreal):
     rho(n,K,norm_inf(n))(psi(n,car(d_b),y0,y,f),psi(n,car(d_b),y0,z,f))
     <=  (Lip_c(n)(f)/K)*(rho(n,K,norm_inf(n))(y,z))

%-------------------------------------------------------------------------
%% The function psi is a contraction
%-------------------------------------------------------------------------

psi_contraction: LEMMA
 FORALL(n:posnat, y0: VectorN(n),
   f: {ff: od_f[d](n) | Lip_d?[d](n)(ff) AND cont_d?(n)(ff)},
   y,z:{yy:r2v_n(n)|continuous?(yy)},K:{KK:posreal|KK>Lip_c(n)(f)}):
     rho(n,K,norm_inf(n))(psi(n,car(d_b),y0,y,f),psi(n,car(d_b),y0,z,f))
     <=  (Lip_c(n)(f)/K)*(rho(n,K,norm_inf(n))(y,z))


%-------------------------------------------------------------------------
%% Is psi has a fixed point the ODE has a unique soulution 
%-------------------------------------------------------------------------

 psi_fixed_imp_sol: LEMMA
    FORALL(n:posnat,  a:{aa:(d)| FORALL(dd:(d)): aa<=dd}, y0: VectorN(n),
    f: {ff: od_f[d](n) | Lip_d?[d](n)(ff) AND cont_d?(n)(ff)}):
     (EXISTS(y:{yy:r2v_n(n)|continuous?(yy)}):
     (psi(n,a,y0,y,f)=y AND FORALL(y2:{yy:r2v_n(n)|continuous?(yy)}):
     psi(n,a,y0,y2,f)=y2 IMPLIES y2=y))
       IMPLIES
     (EXISTS(y:{yy:r2v_n(n)|derivable?(yy)}):
         ivt_s_unique?(n,a,f,y0)(y)
	 AND psi(n,a,y0,y,f)=y)

  %~~~         END   ~~~%
  END ODE_psi_contraction
