ODE_psi_def [ d :[ real -> bool ] ]  % Welcome
% d - domain of the functions r2v
		: THEORY

%Created by                     JTS
%	 	           Jan 2020
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%| Defines the psi function that |%
%| will be a contraction. This   |%
%| will help prove existance and |%
%| uniquesness of first order	 |%
%| ODEs				 |%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%Last Updated	           Jan 2020

%-----     %
  BEGIN
%     -----%

%-------------------------------------------------------------------------
%% Assume that the domain is nonempty, connected, and has more than
%  one element.
%-------------------------------------------------------------------------

  ASSUMING
  
    IMPORTING analysis@deriv_domain_def[(d)]
      non_empty : ASSUMPTION nonempty?(d)
      connected : ASSUMPTION connected?[(d)]
      not_one_element : ASSUMPTION not_one_element?[(d)]
    
  ENDASSUMING

%-------------------------------------------------------------------------
%% Importing ODE_def to have the definition of ODE functions
%-------------------------------------------------------------------------

IMPORTING ODE_def[d]

%-------------------------------------------------------------------------
%% Define psi.This function will be shown to be a contraction and will
%  be an essential part of proving existance and uniqueness of first
%  order ODEs
%-------------------------------------------------------------------------

 psi(n:nat, a:(d), y0: VectorN(n), y:r2v_n(n),
 f:{ff:od_f(n) | continuous?(od_fx(n, ff, y))}):
  {pp:r2v_n(n) | length(pp) = n} =
   LAMBDA(x:(d)): y0 + Integral(a, x, od_fx(n, f, y))

%-------------------------------------------------------------------------
%% psi is y0 at a
%-------------------------------------------------------------------------
       
 psi_init: LEMMA
  FORALL(n:nat, a:(d), y0: VectorN(n), y:r2v_n(n),
  f:{ff:od_f(n) | continuous?(od_fx(n, ff, y))}):
   psi(n, a, y0, y, f)(a) = y0

%-------------------------------------------------------------------------
%% psi is derivable and countinuous
%-------------------------------------------------------------------------

 psi_derivable: LEMMA
  FORALL(n:nat, a:(d), y0: VectorN(n), y:r2v_n(n),
  f:{ff:od_f(n)| continuous?(od_fx(n, ff, y))}):
   derivable?(psi(n, a, y0, y, f))

 psi_cont: LEMMA
  FORALL(n:nat, a:(d), y0: VectorN(n), y:r2v_n(n),
  f:{ff:od_f(n)| continuous?(od_fx(n, ff, y))}):
   continuous?(psi(n, a, y0, y, f))

%-------------------------------------------------------------------------
%% The derivative of psi is the ODE function od_fx
%-------------------------------------------------------------------------

 psi_deriv: LEMMA
  FORALL(n:nat, a:(d), y0: VectorN(n), y:r2v_n(n),
  f:{ff:od_f(n) | continuous?(od_fx(n,ff,y))}):
   deriv(psi(n, a, y0, y, f)) = od_fx(n, f, y)
     
  %~~    END   ~%
  END ODE_psi_def
