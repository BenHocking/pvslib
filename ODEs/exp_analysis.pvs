exp_analysis[d:[real->bool]] % Welcome
% d - domain of e^(c1x+c2)
		: THEORY

%Created by                     JTS
%	 	           Jan 2020
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%| This file shows some          |%
%| properties of e^(c1x+c2),       |%
%| namely, its derivative and    |%
%| intergral. This should be in  |%
%| the analysis or exp_ln part   |%
%| of NASAlib
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%Last updated	           Jan 2020

%-----     %
  BEGIN
%     -----%

%-------------------------------------------------------------------------
%% Assume that the domain is nonempty, connected, and has more than
%  one element.
%-------------------------------------------------------------------------

  ASSUMING
  
    IMPORTING analysis@deriv_domain_def[(d)]
      non_empty : ASSUMPTION nonempty?(d)
      connected : ASSUMPTION connected?[(d)]
      not_one_element : ASSUMPTION not_one_element?[(d)]
    
  ENDASSUMING

 deriv_domain_exp_a: LEMMA
  deriv_domain?[(d)]
%-------------------------------------------------------------------------
%% Importing the chain rule to take the derivative of e^(c1x+c2)
%% Importing the fundemental theorem to connect the derivative
%  and integral
%  Importing lnexp@top to have all properties of e
%------------------------------------------------------------------------

  IMPORTING analysis@chain_rule[(d),real]
  IMPORTING analysis@fundamental_theorem[(d)]
  IMPORTING lnexp@top
  IMPORTING analysis@derivatives_subtype

%-------------------------------------------------------------------------
%% The derivative of e^(c1x-c2)
%-------------------------------------------------------------------------

 derivable_exp_lin: LEMMA
  FORALL(c1, c2:real):
   derivable?[(d)](LAMBDA (x: (d)):
    exp(c1 * x - c2))

 deriv_exp_lin : LEMMA
  FORALL(c1,c2:real):
   deriv(LAMBDA(x:(d)): exp(c1 * x - c2))
   = LAMBDA(x:(d)): c1 * exp(c1 * x - c2)

 derivable_exp_lin_r: LEMMA
  FORALL(c1:{ rr:real | rr /= 0 }, c2:real):
   derivable?[(d)](LAMBDA (x: (d)):
    exp(c1 * x - c2) / c1)

 deriv_exp_lin_r : LEMMA
  FORALL(c1:{ rr:real | rr /= 0 }, c2:real):
   deriv(LAMBDA(x:(d)): exp(c1 * x - c2) / c1)
    = LAMBDA(x:(d)): exp(c1 * x - c2)

%-------------------------------------------------------------------------
%% Continuity of e^(c1x-c2)
%-------------------------------------------------------------------------

 cont_exp_lin: LEMMA
   FORALL(c1, c2:real):
     continuous?[(d)]
      (LAMBDA(x:(d)):exp(c1 * x - c2))

%-------------------------------------------------------------------------
%% The integral of e^(c1x-c2)
%-------------------------------------------------------------------------
     
 deriv_Int_lin: LEMMA
   FORALL(c1:{ rr:real | rr /= 0 }, c2:real,
   a:(d), b:{ bb:(d) | bb >= a }):
    Integral(a, b, LAMBDA(x:(d)): exp(c1 * x - c2))
     =exp(c1 * b - c2) / c1 - exp( c1 * a - c2) / c1

%-------------------------------------------------------------------------
%% The integral of e^(c1x-c2)
%-------------------------------------------------------------------------

 derivable_exp_ln: LEMMA
   FORALL(c1:{ rr:real | rr /= 0 }):
    (FORALL(x:real): d(x) IMPLIES x>0) IMPLIES
     derivable?[(d)](LAMBDA (x: (d)):
      exp(c1 * ln(x)))

 deriv_exp_ln : LEMMA
   FORALL(c1:{ rr:real | rr /= 0 }):
    (FORALL(x:real): d(x) IMPLIES x>0)
     IMPLIES
      deriv( LAMBDA(x:(d)): exp(c1 * ln(x)))
      = (LAMBDA(x:(d)): c1*exp(c1 * ln(x)) / x)

 %~~~    End  ~~%
 END exp_analysis
