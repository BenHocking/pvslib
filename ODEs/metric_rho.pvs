metric_rho [ d :[ real -> bool ] ]  % Welcome
% d - domain of the functions r2v
		: THEORY
		
%Created by                     JTS
%	 	           Jan 2020
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%| This defines a function rho   |%
%| and shows it is a metric on   |%
%| the space of continuous       |%
%| functions on a nonempty       |%
%| closed interval. rho is made  |%
%| using the sup (max since the  |%
%| functions are continuous)     |%
%| norm.                         |%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%Last updated	           Jan 2020

%-----     %
  BEGIN
%     -----%

%-------------------------------------------------------------------------
%% Assume that the domain is a nonempty closed interval
%-------------------------------------------------------------------------

  ASSUMING

    IMPORTING interval_def
      closed_interval : ASSUMPTION closed_interval?(d)

  ENDASSUMING

%-------------------------------------------------------------------------
%% Importing continuous extension to have the extension so the max function
%  can be used
%% Importing metric_def to have the definition of a metric
%% Importing mv_analysis@composition_function to have the fact that
%  the composition of continuous functions is continuous
%-------------------------------------------------------------------------

IMPORTING r2v_continuous_extension[d]
IMPORTING metric_space@metric_def
IMPORTING mv_analysis@composition_function

%-------------------------------------------------------------------------
%% Defining pre-rho 
%-------------------------------------------------------------------------

pre_rho(n:posnat, K:posreal, nn: (norm?(n)))
 (f, g:r2v_n(n))(x: (d)): nnreal
  = exp(-K * (x - car(d_b))) * nn(f(x) - g(x))

%-------------------------------------------------------------------------
%% The pre_rho function is continuous
%-------------------------------------------------------------------------

pre_rho_cont: LEMMA
 FORALL(n:posnat, K:posreal, nn: (norm?(n)),
 f, g:{ff:r2v_n(n)| continuous?(ff)}):
  continuous?[(d)](pre_rho(n, K, nn)(f, g))

pre_rho_cont_D: LEMMA
 FORALL (n: posnat, K: posreal, nn: (norm?(n)),
 f, g: {ff: r2v_n[d](n) | continuous?[d](ff)}):
  contD?[1](LAMBDA (x: VectorN(1)): d(car[real](x)))
   (LAMBDA (x: VectorN(1)):
    ext_c(pre_rho(n, K, nn)(f, g))(car[real](x)))
    
%-------------------------------------------------------------------------
%% Rho is the maximum value of pre_rho
%-------------------------------------------------------------------------

rho(n:posnat, K:posreal, nn:(norm?(n)))
 (f,g:{ff:r2v_n(n)| continuous?(ff)}): nnreal
  = max(LAMBDA(x:VectorN(1)):d(car(x)))
   (LAMBDA(x:VectorN(1)): ext_c(pre_rho(n, K, nn)(f, g))(car(x)))
      
%-------------------------------------------------------------------------
%% Define the space of continuous functions on (d)
%-------------------------------------------------------------------------

S(n:nat): TYPE = { ff:r2v_n(n) | continuous?(ff) }

%-------------------------------------------------------------------------
%% Rho is a metric
%-------------------------------------------------------------------------

rho_zero: LEMMA
  FORALL(n:posnat, K:posreal, nn:(norm?(n))):
    metric_zero?[S(n)](rho(n, K, nn))

rho_symmetric: LEMMA
  FORALL(n:posnat, K:posreal, nn:(norm?(n))):
    metric_symmetric?[S(n)](rho(n, K, nn))

rho_triangle: LEMMA
  FORALL(n:posnat,K:posreal, nn:(norm?(n))):
    metric_triangle?[S(n)](rho(n, K, nn)) 

rho_metric: LEMMA
  FORALL(n:posnat,K:posreal, nn:(norm?(n))):
    metric?[S(n)](rho(n, K, nn))
    
  %~~    End  ~%  
  END metric_rho
