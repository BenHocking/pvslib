norm_ineq [ d :[ real -> bool ] ]  % Welcome
% d - domain of the functions r2v
		: THEORY
%Created by                     JTS
%	 	           Jan 2020
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%| The norm of the definite	 |%
%| intergral is less than or     |%
%| equal to the definite         |%
%| intergral of the norm         |%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%Last Updated	           Jan 2020

%-----     %
 BEGIN
%     -----%

%-------------------------------------------------------------------------
%% Assume that the domain is nonempty, connected, and has more than
%  one element.
%-------------------------------------------------------------------------

  ASSUMING
  
    IMPORTING analysis@deriv_domain_def[(d)]
      non_empty : ASSUMPTION nonempty?(d)
      connected : ASSUMPTION connected?[(d)]
      not_one_element : ASSUMPTION not_one_element?[(d)]
    
   ENDASSUMING

%-------------------------------------------------------------------------
%% Importing continuous_ODE_def to show ODE functions and norms of ODE
%  functions are continuous
%-------------------------------------------------------------------------

IMPORTING ODE_continuous_def[d]

%-------------------------------------------------------------------------
%% the ODE function is integrable for a continuous y, an a f that is
%  continuous in the first variable, and lipchitz in the second
%-------------------------------------------------------------------------

 int_od_fx: LEMMA
  FORALL (a: (d), b: {bb: (d) | bb > a}, n: posnat,
  f: {ff: od_f[d](n) | Lip_d?[d](n)(ff) AND cont_d?(n)(ff)},
  y: {yy: r2v_n[d](n) | continuous?[d](yy)}):
   integrable?[d](a, b, od_fx[d](n, f, y))

 int_norm_od_fx: LEMMA
  FORALL (a: (d), b: {bb: (d) | bb > a}, n: posnat,
  f: {ff: od_f[d](n) | Lip_d?[d](n)(ff) AND cont_d?[d](n)(ff)},
  y: {yy: r2v_n[d](n) | continuous?[d](yy)}):
   integrable?[(d)](a, b,
   LAMBDA (x: (d)): norm_inf(n)(od_fx[d](n, f, y)(x)))

%-------------------------------------------------------------------------
%% Integral and infinity norm inequality specifically for the ODE
%  function
%-------------------------------------------------------------------------

 norminf_int: LEMMA
  FORALL(a:(d), b:{bb:(d)|bb>a},
  n:posnat, f:{ff:od_f[d](n) | Lip_d?[d](n)(ff) AND cont_d?(n)(ff)},
   y:{yy:r2v_n(n) | continuous?(yy)}):
    norm_inf(n)(integral(a, b, od_fx(n, f, y))) <=
      integral(a, b, LAMBDA(x:(d)): norm_inf(n)(od_fx(n, f, y)(x)))

 norminf_Int: LEMMA
   FORALL(a, b:(d), n:posnat,
   f:{ff:od_f[d](n) | Lip_d?[d](n)(ff) AND cont_d?(n)(ff)},
   y:{yy:r2v_n(n) | continuous?(yy)}):
    a < b IMPLIES (norm_inf(n)(Integral(a, b, od_fx(n, f, y))) <=
     Integral(a, b, LAMBDA(x:(d)):norm_inf(n)(od_fx(n, f, y)(x))))
       
%-------------------------------------------------------------------------
%% Now for the general continuous function y, the norm of the function
%  is integrable 
%-------------------------------------------------------------------------

 int_norm_cont: LEMMA
   FORALL (a: (d), b: {bb: (d) | bb > a}, n: posnat,
   y: {yy: r2v_n[d](n) | continuous?[d](yy)}):
    integrable?[(d)](a, b,
    LAMBDA (x: (d)): norm_inf(n)(y(x)))

%-------------------------------------------------------------------------
%% For a general continuous function y, the norm of the integral of y
%  is less than or equal to the integral of the norm
%-------------------------------------------------------------------------

  normi: LEMMA
   FORALL(a,b:(d),n:posnat, y:{yy:r2v_n(n)| continuous?(yy)}):
    a<b IMPLIES (norm_inf(n)(integral(a,b,y))) <=
     integral(a, b, LAMBDA(x:(d)):norm_inf(n)(y(x)))

  normI: LEMMA
   FORALL(a,b:(d),n:posnat, y:{yy:r2v_n(n)| continuous?(yy)}):
    a <= b IMPLIES (norm_inf(n)(Integral(a,b,y))) <=
     Integral(a, b, LAMBDA(x:(d)):norm_inf(n)(y(x)))
       
  %~~   END  ~%
  END norm_ineq
