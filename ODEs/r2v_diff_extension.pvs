r2v_diff_extension [ d :[ real -> bool ] ]  % Welcome
% d - domain of the functions r2v
		: THEORY

%Created by                     JTS
%	 	           Jan 2020
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%| The differentiable extension  |%
%| of a function from (d)->real  |%
%| to [real -> real] is defined  |%
%| here. With useful properties  |%
%| This should be moved to       |%
%| analysis or real in the       |%
%| NASAlib.		         |%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%Last Updated	           Feb 2020
%			        JTS

%-----     %
  BEGIN
%     -----%

%-------------------------------------------------------------------------
%% Assume that the domain is a nonempty closed interval
%-------------------------------------------------------------------------

  ASSUMING

    IMPORTING interval_def
      closed_interval : ASSUMPTION closed_interval?(d)

  ENDASSUMING

%-------------------------------------------------------------------------
%% Importing the continuous extension to have all the r2v_analysis
%  and the definition of d_b
%-------------------------------------------------------------------------

IMPORTING r2v_continuous_extension[d]

%-------------------------------------------------------------------------
%% The next lemmas are preperation to make the differentiable extension
%-------------------------------------------------------------------------

%-------------------------------------------------------------------------
%% The extension is differentiable at the lower boundary element of d,
%  at has the same derivative as the original function 
%-------------------------------------------------------------------------

ext_derivable_lb: LEMMA
  FORALL(f:{ff:[(d)->real]| derivable?[(d)](ff)}):
  LET g = LAMBDA(x:real):
   IF x < car(d_b)
     THEN deriv[(d)](f)(car(d_b))*(x-car(d_b)) + f(car(d_b))
   ELSIF x > car(cdr(d_b))
     THEN deriv[(d)](f)(car(cdr(d_b)))*(x-car(cdr(d_b))) + f(car(cdr(d_b)))
   ELSE f(x)
   ENDIF IN
   derivable?[real](g,car(d_b))

ext_deriv_lb: LEMMA
  FORALL(f:{ff:[(d)->real]| derivable?[(d)](ff)}):
  LET g = LAMBDA(x:real):
   IF x < car(d_b)
     THEN deriv[(d)](f)(car(d_b))*(x-car(d_b)) + f(car(d_b))
   ELSIF x > car(cdr(d_b))
     THEN deriv[(d)](f)(car(cdr(d_b)))*(x-car(cdr(d_b))) + f(car(cdr(d_b)))
   ELSE f(x)
   ENDIF IN
   deriv[real](g,car(d_b)) = deriv[(d)](f)(car(d_b))

%-------------------------------------------------------------------------
%% The extension is differentiable at the upper boundary element of d,
%  at has the same derivative as the original function 
%-------------------------------------------------------------------------

ext_derivable_ub: LEMMA
  FORALL(f:{ff:[(d)->real]| derivable?[(d)](ff)}):
  LET g = LAMBDA(x:real):
   IF x < car(d_b)
     THEN deriv[(d)](f)(car(d_b))*(x-car(d_b)) + f(car(d_b))
   ELSIF x > car(cdr(d_b))
     THEN deriv[(d)](f)(car(cdr(d_b)))*(x-car(cdr(d_b))) + f(car(cdr(d_b)))
   ELSE f(x)
   ENDIF IN
   derivable?[real](g,car(cdr(d_b)))

ext_deriv_ub: LEMMA
  FORALL(f:{ff:[(d)->real]| derivable?[(d)](ff)}):
  LET g = LAMBDA(x:real):
   IF x < car(d_b)
     THEN deriv[(d)](f)(car(d_b))*(x-car(d_b)) + f(car(d_b))
   ELSIF x > car(cdr(d_b))
     THEN deriv[(d)](f)(car(cdr(d_b)))*(x-car(cdr(d_b))) + f(car(cdr(d_b)))
   ELSE f(x)
   ENDIF IN
   deriv[real](g,car(cdr(d_b))) = deriv[(d)](f)(car(cdr(d_b)))

%-------------------------------------------------------------------------
%% The extension is differentiable on the interior of d, at has
%  the same derivative as the original function 
%-------------------------------------------------------------------------

ext_derivable_int: LEMMA
  FORALL(f:{ff:[(d)->real]| derivable?[(d)](ff)},
  t: {tt:real | car(d_b) < tt AND tt < car(cdr(d_b))}):
  LET g = LAMBDA(x:real):
   IF x < car(d_b)
     THEN deriv[(d)](f)(car(d_b))*(x-car(d_b)) + f(car(d_b))
   ELSIF x > car(cdr(d_b))
     THEN deriv[(d)](f)(car(cdr(d_b)))*(x-car(cdr(d_b))) + f(car(cdr(d_b)))
   ELSE f(x)
   ENDIF IN
   derivable?[real](g,t)

ext_deriv_int: LEMMA
  FORALL(f:{ff:[(d)->real]| derivable?[(d)](ff)},
  t: {tt:real | car(d_b) < tt AND tt < car(cdr(d_b))}):
  LET g = LAMBDA(x:real):
   IF x < car(d_b)
     THEN deriv[(d)](f)(car(d_b))*(x-car(d_b)) + f(car(d_b))
   ELSIF x > car(cdr(d_b))
     THEN deriv[(d)](f)(car(cdr(d_b)))*(x-car(cdr(d_b))) + f(car(cdr(d_b)))
   ELSE f(x)
   ENDIF IN
   deriv[real](g,t) = deriv(f,t)

%-------------------------------------------------------------------------
%% The extension is differentiable on the exterior of d, at has
%  the same derivative as the original function on the upper
%  and lower boundary of d
%-------------------------------------------------------------------------

ext_derivable_ext: LEMMA
  FORALL(f:{ff:[(d)->real]| derivable?[(d)](ff)},
  t: {tt:real | car(d_b) > tt OR tt > car(cdr(d_b))}):
  LET g = LAMBDA(x:real):
   IF x < car(d_b)
     THEN deriv[(d)](f)(car(d_b))*(x-car(d_b)) + f(car(d_b))
   ELSIF x > car(cdr(d_b))
     THEN deriv[(d)](f)(car(cdr(d_b)))*(x-car(cdr(d_b))) + f(car(cdr(d_b)))
   ELSE f(x)
   ENDIF IN
   derivable?[real](g,t)

ext_deriv_extl: LEMMA
  FORALL(f:{ff:[(d)->real]| derivable?[(d)](ff)},
  t: {tt:real | car(d_b) > tt}):
  LET g = LAMBDA(x:real):
   IF x < car(d_b)
     THEN deriv[(d)](f)(car(d_b))*(x-car(d_b)) + f(car(d_b))
   ELSIF x > car(cdr(d_b))
     THEN deriv[(d)](f)(car(cdr(d_b)))*(x-car(cdr(d_b))) + f(car(cdr(d_b)))
   ELSE f(x)
   ENDIF IN
   deriv[real](g,t) =  deriv[(d)](f)(car(d_b))

ext_deriv_extu: LEMMA
  FORALL(f:{ff:[(d)->real]| derivable?[(d)](ff)},
  t: {tt:real | car(cdr(d_b)) < tt}):
  LET g = LAMBDA(x:real):
   IF x < car(d_b)
     THEN deriv[(d)](f)(car(d_b))*(x-car(d_b)) + f(car(d_b))
   ELSIF x > car(cdr(d_b))
     THEN deriv[(d)](f)(car(cdr(d_b)))*(x-car(cdr(d_b))) + f(car(cdr(d_b)))
   ELSE f(x)
   ENDIF IN
   deriv[real](g,t) =  deriv[(d)](f)(car(cdr(d_b)))

%-------------------------------------------------------------------------
%% Define the differentiable extension of a differentiable function on d
%  to all of the reals
%-------------------------------------------------------------------------

ext_d(f:{ff:[(d)->real]| derivable?[(d)](ff)}):
 {ff:[real -> real] | derivable?[real](ff)
 AND
 (FORALL(x:(d)):  ff(x) = f(x) AND deriv[real](ff,x) = deriv[(d)](f,x))}
 = LAMBDA(x:real):
   IF x < car(d_b)
     THEN deriv[(d)](f)(car(d_b))*(x-car(d_b)) + f(car(d_b))
   ELSIF x > car(cdr(d_b))
     THEN deriv[(d)](f)(car(cdr(d_b)))*(x-car(cdr(d_b))) + f(car(cdr(d_b)))
   ELSE f(x)
   ENDIF
   
  %~~~       END    ~~~%
  END r2v_diff_extension
