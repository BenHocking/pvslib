continuity_props  % [ parameters ]
		: THEORY

  BEGIN

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%| Continuity properties for   |%
%| real expressions            |%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


  % ASSUMING
   % assuming declarations
  % ENDASSUMING

  IMPORTING continuity_re_def,
  	    analysis@continuity_props,
	    ODEs_equiv


%-------------------------------------------------------------------------
%% solution to an ode is continuous
%-------------------------------------------------------------------------

 f_sol_continuous: LEMMA
  FORALL(D:(dd?),envi:Environment,
  ode:ODEs,fs:[below(length(ode)) -> [real -> real]]):
  solution_odes?(D,ode,envi)(fs)
  IMPLIES
  FORALL(i:below(length(ode))): continuous?[(D)](fs(i))

 c_delt(D:(dd?),x:(D), f:{ff:[(D)->real] | continuous?[(D)](ff)})(eps:posreal):
  {delta:posreal | FORALL(x1:(D)): abs(x1-x)<delta IMPLIES abs(f(x1)-f(x)) < eps}

  min_delt(D:(dd?),x:(D),n:posnat,
   fs:[below(n) -> [real -> real]] | FORALL(i:below(n)): continuous?[(D)](fs(i)))(eps:posreal):
   RECURSIVE {DELTA:posreal|
    FORALL(i:below(n),x1:(D)): abs(x1-x) < DELTA
    IMPLIES abs(fs(i)(x1)-fs(i)(x)) < eps} =
    IF n = 1 THEN c_delt(D,x,fs(0))(eps)
    ELSE
    min(  c_delt(D,x,fs(0))(eps),
    min_delt(D,x,n-1,LAMBDA(i:below(n-1)): fs(i+1))(eps))
    ENDIF
    MEASURE n

%-------------------------------------------------------------------------
%% env_at_t for a solution fs of an ode is continuous
%-------------------------------------------------------------------------

  env_at_t_continuous: LEMMA
   FORALL(D:(dd?),envi:Environment,
    ode:ODEs,fs:[below(length(ode)) -> [real -> real]]):
      solution_odes?(D,ode,envi)(fs)
      IMPLIES
      FORALL(i:nat):
      continuous_env?(D,LAMBDA(t:(D)): env_at_t(ode,fs,envi)(t))

%-------------------------------------------------------------------------
%% continuous real expression evaluated at an env_at_t is continuous
%-------------------------------------------------------------------------

   % @QED re_env_continuous proved by lmwhite3 on Tue, 06 Sep 2022 20:28:33 GMT
  re_env_continuous: LEMMA
   FORALL(P:(ddl_dom?), re:(cont_re?(P)), D:(dd?), envi:Environment,ode:ODEs,fs:[below(length(ode)) -> [real -> real]]):
   (solution_odes?(D,ode,envi)(fs)
   AND
   FORALL(t:(D)): P(env_at_t(ode,fs,envi)(t)))
   IMPLIES
   continuous?[(D)](LAMBDA(t:(D)): re(env_at_t(ode,fs,envi)(t)))
    
  END continuity_props
