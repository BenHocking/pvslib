Cauchy_seq [ n:posnat ]  % [ parameters ]
		: THEORY

  BEGIN

IMPORTING vector_sequences[n]
IMPORTING metric_space@euclidean[n]
IMPORTING Vector_VectorN[n]
IMPORTING metric_space@metric_space_def

%--------------------------------------------
%  Convergence of vector sequence to vector
%--------------------------------------------

cauchy?(m: posnat)(V: vec_seq(m)):
     bool =
     FORALL( nn: (norm?(m))):
     FORALL (epsilon: posreal) : EXISTS (N: nat) : 
	    FORALL (MM,NN: above(N)):  nn(V(NN) - V(MM)) < epsilon

cauchy?(m: posnat, nn: (norm?(m)))(V: vec_seq(m)):
     bool =
     FORALL (epsilon: posreal) : EXISTS (N: nat) : 
	    FORALL (MM,NN: above(N)):  nn(V(NN) - V(MM)) < epsilon

cauchy?(V: vec_seq(n)):
     bool = cauchy?(n)(V)

cauchy?(nn: (norm?(n)))(V: vec_seq(n)):
     bool =
     cauchy?(n,nn)(V)

cauchy_equiv: LEMMA
     FORALL(V:vec_seq(n)):
     (EXISTS(nn:(norm?(n))):
     cauchy?(n, nn)(V))
     IFF
     cauchy?(n)(V)

cauchy_V_Vn: LEMMA
     FORALL(V:vec_seq(n)):
     cauchy?(Vn_2_V(n,V))
     IFF
     cauchy?(n)(V)


cauchy_convergence: LEMMA
     FORALL(V:vec_seq(n)):
     cauchy?(n)(V)
     IMPLIES
     EXISTS(L:VectorN(n)): convergence?(n)(V,L)

  END Cauchy_seq
