Vector_VectorN[n:posnat] %Hello
%n - vector sequences in Rn
		: THEORY
		
%	               Jan 2020 JTS
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%| Shows connection between       |%
%| Vectors as lists (VectorN[n]) |%
%| and Vectors as arrays         |%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%		

%-----     %
  BEGIN
%     -----%

%--------------------------------------------
%% Importing euclidea to have definitions
%  related to Vectors as arrays

%% Importing vector_sequences to have
%  definitions related to Vectors as lists
%--------------------------------------------

 IMPORTING metric_space@euclidean
 IMPORTING vector_sequences[n]
 IMPORTING reals@sigma_below_sub

%--------------------------------------------
%%Relationship between vectors as lists
% and vectors as arrays
%--------------------------------------------

Vn_2_V(m:posnat, V:VectorN(m)):
     vectors@vectors[m].Vector
     = LAMBDA(i:below(m)): nth(V,i)

V_2_Vn(m:posnat,V:vectors@vectors[m].Vector): RECURSIVE
     {VV:VectorN(m) | FORALL(i:below(m)): nth(VV,i) = V(i)}
     = IF m=1 THEN (: V(0) :)
     ELSE
     cons(V(0), V_2_Vn(m-1,LAMBDA(i:below(m-1)): V(i+1)))
     ENDIF
     MEASURE m

Vn_V: LEMMA
     FORALL(m:posnat, V:VectorN(m)):
     V_2_Vn(m,Vn_2_V(m,V)) = V

V_Vn: LEMMA
     FORALL(m:posnat, V:vectors@vectors[m].Vector):
     Vn_2_V(m,V_2_Vn(m,V)) = V

%--------------------------------------------
%%Relationship between sequences of vectors
% as lists and sequences vectors as arrays
%--------------------------------------------

Vn_2_V(m:posnat,
     V:vec_seq(m)): [nat -> vectors@vectors[m].Vector]
     = LAMBDA(i:nat): Vn_2_V(m, V(i))

V_2_Vn(m:posnat,
     V:[nat-> vectors@vectors[m].Vector]): vec_seq(m)
     = LAMBDA(i:nat): V_2_Vn(m, V(i))

Vn_V_seq: LEMMA
     FORALL(m:posnat, V:vec_seq(m)):
     V_2_Vn(m,Vn_2_V(m,V)) = V
     
V_Vn_seq: LEMMA
     FORALL(m:posnat,
     V:[nat-> vectors@vectors[m].Vector]):
     Vn_2_V(m,V_2_Vn(m,V)) = V


%--------------------------------------------
%%relationship between the two norm (lists)
% and "d2" (array)
%--------------------------------------------

d2_sigma: LEMMA
     FORALL(m:posnat,f,g:[below(m)->real]):
     d2[m](f,g) = sqrt(sigma(0, m-1,
     LAMBDA (i: nat): IF i<m THEN sq(f(i)-g(i)) ELSE 0 ENDIF))

norm2_d2: LEMMA
     FORALL(m:posnat, V1,V2: VectorN(m)):
     norm_2(m)(V1-V2) = d2[m](Vn_2_V(m,V1),Vn_2_V(m,V2))


%--------------------------------------------
%%relationship between convergence of
% vector sequence
%--------------------------------------------

convergence_V_Vn: LEMMA
     FORALL(m:posnat, V: vec_seq(m), L:VectorN(m)):
     convergence?(m)(V,L)
     IFF
     metric_converges_to[Vector[m],d2[m]]
     (Vn_2_V(m, V), Vn_2_V(m,L))

  END Vector_VectorN
