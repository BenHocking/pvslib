max_function  [n:posnat] %Greetings
%n - length of vectors we are using
		: THEORY

%	               Jan 2020 JTS
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%| the max function over a domain|%
%| is defined here.		 |%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%-----     %
  BEGIN
%     -----%

%--------------------------------------------
%% Importing extreme_value_theorem[ n ]
%  to show max is defined for a continuous
%  function on a closed domain
%--------------------------------------------

IMPORTING extreme_value_theorem[ n ] 

%--------------------------------------------
%% define max with all generality
%--------------------------------------------

max_gen(D:domain)(f:{ff:vec2real_f |
  EXISTS(c:real): is_glob_max?(ff,D)(c) AND EXISTS(v:(D)): ff(v)=c }):
  {c:real| is_glob_max?(f, D)(c) AND EXISTS(v:(D)): f(v)=c}

%--------------------------------------------
%% define max on closed domain
%  with continuous function
%--------------------------------------------

max(D:{DD:(closed_domain?)|
  bounded_domain?(DD) AND
  nonempty?(DD)})
  (f:{ff:vec2real_f | contD?(D)(ff)}):
  {c:real| is_glob_max?(f, D)(c) AND EXISTS(v:(D)): f(v)=c}
  = max_gen(D)(f)

%--------------------------------------------
%% properties of max
%--------------------------------------------

max_plus: LEMMA
  FORALL(D:{DD:(closed_domain?)|
  bounded_domain?(DD) AND
  nonempty?(DD)}, f,g :{ff:vec2real_f | contD?(D)(ff)}):
  max(D)(f+g) <= max(D)(f) + max(D)(g)
  
max_ineq: LEMMA
  FORALL(D:{DD:(closed_domain?)|
  bounded_domain?(DD) AND
  nonempty?(DD)}, f,g :{ff:vec2real_f | contD?(D)(ff)}):
  (FORALL(x:(D)): f(x) <= g(x)) IMPLIES max(D)(f) <= max(D)(g)

max_const: LEMMA
  FORALL(D:{DD:(closed_domain?)|
  bounded_domain?(DD) AND
  nonempty?(DD)}, f :{ff:vec2real_f | contD?(D)(ff)},
  C:nnreal):
  max(D)(C*f) =C*max(D)(f)

  END max_function
