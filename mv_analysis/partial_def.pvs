partial_def   [ n:posnat ] % Namaste
%% n - partial derivative of f: Rn -> R
		: THEORY

%	              Aug 2018 JTS
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%|      Defines partial          |%
%|         derivatives           |%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%-----     %
  BEGIN
%     -----%

%--------------------------------------------
%% Importing directional_deriv_def[n] to define
%  partial derivative 
%--------------------------------------------
  
IMPORTING directional_deriv_def[n]

%--------------------------------------------
%% define the ith partial
%--------------------------------------------

partial(i: below( n ), D: (deriv_domain_partial?(e(n,i))),
     f: vec2real_f, p: (init_partial?(e(n,i), D, f))):
     real =
     init_partial(e( n, i ), D , f )( p )

partial(i: below( n ), D: (deriv_domain_partial?(e(n,i))),
     f: (init_partial?(e(n,i), D) )) 
     (p: ( D ) ):
     real =
     partial(i,D,f,p)

%--------------------------------------------
%% function for showing direcitonal derivatives
%  can be decomposed as sum of partials
%--------------------------------------------

F_partials(D:(deriv_domain_e_partial?),
     v:VectorN(n), f: (differentiableD?(D)))(p:(D))(i:nat):
     real =
     IF i < n
     THEN nth(v,i)*partial(i, D, f )(p)
     ELSE 0
     ENDIF

F_partials_e: LEMMA
 FORALL(D:(deriv_domain_e_partial?), p:(D), i:below(n), f: (differentiableD?(D))):
 F_partials(D,e(n,i),f)(p) = LAMBDA(j:nat): IF j = i THEN partial(i,D,f)(p) ELSE 0 ENDIF

%--------------------------------------------
%% Show directional_deriv can be decomposed
%  in terms of sum of partials
%--------------------------------------------

directional_deriv_pre1: LEMMA FORALL(D:(deriv_domain_all_partial?), v:VectorN(n),
     f: (differentiableD?(D)), p:(D)):
     FORALL(i: below(n)):
     init_partial(Standard_sum(v,i),D,f)(p) = sigma(0,i, F_partials(D,v,f)(p))

directional_deriv_sum_partials: LEMMA FORALL(D: (deriv_domain_all_partial?),
     v: VectorN( n ), f: (differentiableD?(D)), p:(D)):
    init_partial(v, D, f)(p) = sigma(0, n-1, F_partials(D,v,f)(p))
    
%________________________________________________________
 % "In three words I can sum up everything
 %     I've learned about life: it goes on."
 %	       	  	       	           - Robert Frost 

  %  ~The End~  %
  END partial_def
