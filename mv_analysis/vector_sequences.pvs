vector_sequences   [ n: posnat ] %Tungjatjeta
%n - vector sequences in Rn
		: THEORY
		
%	              Aug 2018 JTS
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%|     define convergence        |%
%|      and unboundedness        |%
%|     for vector sequence       |%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%		

%-----     %
  BEGIN
%     -----%

%--------------------------------------------
%% Importing norm_equiv and norm_equiv_general
%  to get convergence invariant of norm

%% Importing domain_ve2real_def to have
%  vector sequences
%--------------------------------------------

IMPORTING norms_equiv_general
IMPORTING norms_equiv[n]
IMPORTING domain_vec2real_def[n]


%--------------------------------------------
%%Convergence of vector sequence to vector
%--------------------------------------------

convergence?(m: posnat)(V: vec_seq(m), L: VectorN(m)):
     bool =
     FORALL( nn: (norm?(m))):
     FORALL (epsilon: posreal) : EXISTS (N: posnat) : 
	    FORALL (NN: above(N)):  nn(V(NN) - L) < epsilon

convergence?(m: posnat, nn: (norm?(m)))(V: vec_seq(m), L: VectorN(m)):
     bool =
     FORALL (epsilon: posreal) : EXISTS (N: posnat) : 
	    FORALL (NN: above(N)):  nn(V(NN) - L) < epsilon

convergence?(V: vec_seq(n), L: VectorN(n)):
     bool = convergence?(n)(V,L)

convergence?(nn: (norm?(n)))(V: vec_seq(n), L: VectorN(n)):
     bool =
     convergence?(n,nn)(V,L)

convergence_equiv_m: LEMMA FORALL(m:posnat, V:vec_seq(m), L:VectorN(m)):
     (EXISTS(nn:(norm?(m))):
     convergence?(m, nn)(V, L))
     IFF
     convergence?(m)(V,L)

%--------------------------------------------
%%Convergence is equivalent over all norms
%--------------------------------------------
convergence_equiv: LEMMA FORALL(V:vec_seq(n), L:VectorN(n)):
     (EXISTS(nn:(norm?(n))):
     convergence?(nn)(V, L))
     IFF
     convergence?(V,L)

%--------------------------------------------
%%These are old definitions of convergence
% need to be deleted but I'm not sure if
% they are used somewhere that needs attention
%--------------------------------------------

convergence?(m: posnat, V: vec_seq(m), L: VectorN(m)): bool = 
	FORALL (epsilon: posreal) : EXISTS (N: posnat) : 
	    FORALL (NN: above(N)):  norm_inf(m)(V(NN) - L) < epsilon

convergence_zero?(D:domain, f:vec2real_f, p: VectorN(n)): bool =
	FORALL (epsilon: posreal) : EXISTS (delta: posreal) : 
	    FORALL(x: {xx: (D)| xx /= p}): norm_inf(n)(x-p) < delta
	    IMPLIES abs( f( x ) ) < epsilon

%convergence_zero?(m:posnat, D:domain, f:[VectorN(n) -> VectorN(m)], p: VectorN(n)): bool =
%	FORALL (epsilon: posreal) : EXISTS (delta: posreal) : 
%	    FORALL(x: {xx: (D)| xx /= p}): norm_inf(n)(x-p) < delta
%	    IMPLIES norm_inf(m)( (f(x) ) ) < epsilon

convergence_unique_vec: LEMMA FORALL(m:posnat, V:vec_seq(m), L1, L2:VectorN(m)):
     convergence?(m,V,L1) AND convergence?(m,V,L2)
     IMPLIES
     L1=L2

test_conv: LEMMA FORALL ( V: vec_seq( 3 ) ):
     (FORALL(m:posnat):
     V( m ) = (: 1/m , 1/m, 1/m :) )
     IMPLIES
     convergence?( 3, V,  (: 0, 0, 0 :) )

%--------------------------------------------
%%Convergence is unique
%--------------------------------------------

convergence_unique_vec_seq: LEMMA FORALL(m:posnat, V:vec_seq(m), L1, L2:VectorN(m)):
     convergence?(m)(V,L1) AND convergence?(m)(V,L2)
     IMPLIES
     L1=L2

%--------------------------------------------
%%Define unboundedness of a vector sequence
%--------------------------------------------

unbounded?(m: posnat)(V:vec_seq(m)): bool =
     FORALL( nn:(norm?(m))):
     FORALL (M: nonneg_real): EXISTS (N:posnat):
     nn(V(N)) > M

unbounded?(m: posnat, nn:(norm?(m)))(V:vec_seq(m)): bool =
     FORALL (M: nonneg_real): EXISTS (N:posnat):
     nn(V(N)) > M
     
%--------------------------------------------
%%unboundedness is equivalent across norms
%--------------------------------------------

unbounded_equiv: LEMMA FORALL(m:posnat, V:vec_seq(m), nn:(norm?(m))):
     unbounded?(m)(V)
     IFF
     unbounded?(m,nn)(V)

%--------------------------------------------
%%This is the old of unboundedness
% need to be deleted but I'm not sure if
% it is used somewhere that needs attention
%--------------------------------------------

unbounded?(m: posnat, V:vec_seq(m)): bool =
  	FORALL (M: nat): EXISTS (N:posnat):
	norm_inf(m)(V(N)) > M

%--------------------------------------------
%% A sequence converges iff each entry converges
%--------------------------------------------

nth_N: LEMMA
  FORALL(m:nat,V:vec_seq(m), L: VectorN(m)):
  (FORALL(i:{ii:nat| ii< m}):
  convergence(LAMBDA(j:nat): nth(V(j),i),nth(L,i)))
  IMPLIES
  (FORALL(epsilon:posreal):
  EXISTS(N:nat):
  FORALL(j:{jj:nat| jj>N},i:{ii:nat| ii< m}):
  abs(nth(V(j),i)-nth(L,i))<epsilon)
  
converge_nth: LEMMA
  FORALL(V:vec_seq(n), L: VectorN(n)):
  convergence?(V, L)
  IFF
  FORALL(i:{ii:nat| ii< n}):
  convergence(LAMBDA(j:nat): nth(V(j),i),nth(L,i))

%________________________________________________________
 % "The essence of mathematics lies entirely in its freedom."
 %	       	  	   	    â€“ Georg Cantor

  %--   ~The End~  --%
  END vector_sequences
